// Copyright 2000-2019 JetBrains s.r.o. Use of this source code is governed by the Apache 2.0 license that can be found in the LICENSE file.
apply plugin: 'java'
apply plugin: 'maven-publish'

compileJava {
  sourceCompatibility '1.8'
  targetCompatibility '1.8'
}

group = 'org.jetbrains'
archivesBaseName = 'intellij-fernflower'

version = '1.2.0'

def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
    version = version + "." + "${ENV.BUILD_NUMBER}"
}

sourceSets {
  main.java.srcDirs 'src'
  test.java.srcDirs 'test'
}

repositories { jcenter() }
dependencies {
  testCompile 'junit:junit:4.12'
  testCompile 'org.assertj:assertj-core:3.12.2'
}

jar {
  manifest {
    attributes 'Main-Class': 'org.jetbrains.java.decompiler.main.decompiler.ConsoleDecompiler'
  }
}

task sourceJar(type:Jar) {
  classifier = "sources"
  from sourceSets.main.allSource
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact(sourceJar)
    }
  }

  // select the repositories you want to publish to
  repositories {
    if (project.hasProperty('mavenPass')) {
      maven {
        url = "http://mavenupload.modmuss50.me/"
        credentials {
          username = "buildslave"
          password = project.getProperty('mavenPass')
        }
      }
    }
  }
}